apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.ofg.uptodate'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../AndroidCodeQualityConfig/quality.gradle'

/*def keyConfigPath
if (isFamily(FAMILY_WINDOWS)) {
    keyConfigPath = System.getenv('USERPROFILE') + File.separator + ".ssh" + File.separator +
            "TemplateKeyStore.properties"
} else {
    keyConfigPath = System.getenv('HOME') + File.separator + ".ssh" + File.separator +
            "TemplateKeyStore.properties"
}

Properties props = new Properties()
if (new File(keyConfigPath).exists()) {
    props.load(new FileInputStream(file(keyConfigPath)))
}*/

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        applicationId "com.github.piasy.template"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        //multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "REPORT_CRASH", "false"
        buildConfigField "boolean", "INSTALL_LEAK_CANARY", "false"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    signingConfigs {
        develop {
            storeFile file('../buildsystem/OpenKey.jks')
            storePassword '123456'
            keyAlias '123456'
            keyPassword '123456'
        }

        /*production {
            storeFile file(props['keystore'])
            storePassword props['keystore.password']
            keyAlias props['key.alias']
            keyPassword props['key.password']
        }*/
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.develop
        }

        release {
            minifyEnabled true
            debuggable true
            shrinkResources true
            signingConfig signingConfigs.develop // use open sign key
            //signingConfig signingConfigs.production  // use private sign key
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                    'proguard-fresco.pro'
        }
    }

    productFlavors.whenObjectAdded { flavor ->
        def flavorData = rootProject.ext[flavor.name]
    }

    productFlavors {
        develop {
            buildConfigField "boolean", "REPORT_CRASH", "false"
        }

        product {
            buildConfigField "boolean", "REPORT_CRASH", "true"
        }
    }
    sourceSets {
        test.java.srcDirs += 'build/generated/source/apt/develop/debug'
    }
}

unMock {
    // URI to download the android-all.jar from. e.g. https://oss.sonatype
    // .org/content/groups/public/org/robolectric/android-all/
    downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/4' +
            '.3_r2-robolectric-0/android-all-4.3_r2-robolectric-0.jar'

    keep "android.text.TextUtils"
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies

    compile (project(':model')) {
        exclude module: 'xlog'
    }
    compile project(':EasyFlow')
    compile (presentationDependencies.androidSupportAppCompatV7) {
        exclude module: 'support-v4'
    }
    compile (presentationDependencies.supportDesignLibrary) {
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
    }
    compile presentationDependencies.butterKnife
    compile (presentationDependencies.rxAndroid) {
        exclude module: 'rxjava'
    }

    apt presentationDependencies.daggerCompiler

    compile presentationDependencies.supportAnnotation
    compile (presentationDependencies.mvp) {
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
    }
    compile (presentationDependencies.once) {
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
    }
    compile (presentationDependencies.rxBinding) {
        exclude module: 'support-annotations'
        exclude module: 'rxjava'
    }
    compile (presentationDependencies.rxBindingAppCompatV7) {
        exclude module: 'appcompat-v7'
        exclude module: 'rxbinding'
    }
    compile (presentationDependencies.rxLifeCycle) {
        exclude module: 'rxbinding'
        exclude module: 'rxjava'
    }
    compile (presentationDependencies.rxLifeCycleComponent) {
        exclude module: 'rxlifecycle'
        exclude module: 'appcompat-v7'
        exclude module: 'rxjava'
    }

    // UI library
    compile (presentationDependencies.recyclerViewV7) {
        exclude module: 'support-v4'
    }
    compile presentationDependencies.cardViewV7
    compile (presentationDependencies.fresco) {
        exclude module: 'support-v4'
    }
    compile presentationDependencies.loadToast
    compile (presentationDependencies.iconifyMD) {
        exclude module: 'support-v4'
    }
    compile (presentationDependencies.mugen) {
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    // Developer
    developCompile (presentationDependencies.xLogAndroid) {
        exclude module: 'gson'
    }
    productCompile presentationDependencies.xLogAndroidIdle
    developCompile presentationDependencies.leakCanary
    productCompile presentationDependencies.leakCanaryIdle
    retrolambdaConfig presentationDependencies.retrolambda
    compile (presentationDependencies.fabric) {
        transitive = true
    }

    androidTestCompile (presentationTestDependencies.mockito) {
        exclude module: 'hamcrest-core'
    }
    androidTestCompile presentationTestDependencies.hamcrestLibrary
    androidTestCompile presentationTestDependencies.dexmaker
    androidTestCompile (presentationTestDependencies.dexmakerMockito) {
        exclude module: 'hamcrest-core'
        exclude module: 'mockito-core'
    }
    androidTestCompile (presentationTestDependencies.espresso) {
        exclude module: 'hamcrest-library'
        exclude module: 'rules'
        exclude module: 'runner'
    }
    androidTestCompile (presentationTestDependencies.espessoContrib) {
        exclude module: 'espresso-core'
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'hamcrest-core'
    }
    androidTestCompile (presentationTestDependencies.espressoIntents) {
        exclude module: 'espresso-core'
    }
    androidTestCompile (presentationTestDependencies.androidJUnit4Rules) {
        exclude module: 'support-annotations'
        exclude module: 'hamcrest-core'
    }
    androidTestApt presentationDependencies.daggerCompiler
}
